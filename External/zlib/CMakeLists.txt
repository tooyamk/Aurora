__set_project()
if (UNIX)
    set(__OUTPUT_NAME libz)
else ()
    set(__OUTPUT_NAME zlib)
endif ()
set_property(GLOBAL PROPERTY __PROJECT_ZLIB_NAME ${PROJECT_NAME})
set_property(GLOBAL PROPERTY __PROJECT_ZLIB_INCLUDE ${PROJECT_BINARY_DIR}/install/include)
set_property(GLOBAL PROPERTY __PROJECT_ZLIB_LIB ${PROJECT_BINARY_DIR}/install/lib/${__OUTPUT_NAME}$<$<CONFIG:Debug>:d>${__AE_LIB_SUFFIX})
set_property(GLOBAL PROPERTY __PROJECT_ZLIB_DLL ${PROJECT_BINARY_DIR}/install/$<IF:$<PLATFORM_ID:Windows>,bin,lib>/${__OUTPUT_NAME}$<$<CONFIG:Debug>:d>$<$<PLATFORM_ID:Windows>:1>${__AE_DLL_SUFFIX})

include(ExternalProject)

ExternalProject_Add(
    ${PROJECT_NAME}
    PREFIX ${PROJECT_BINARY_DIR}
    GIT_REPOSITORY https://github.com/madler/zlib.git
    GIT_TAG v1.2.11
    GIT_SHALLOW 1
    GIT_PROGRESS 1
    TMP_DIR tmp
    STAMP_DIR stamp
    DOWNLOAD_DIR download
    INSTALL_DIR install
    SOURCE_DIR src
    BINARY_DIR ${__BUILD_DIR}
    UPDATE_COMMAND ""
    BUILD_BYPRODUCTS 
        ${PROJECT_BINARY_DIR}/install/lib/${__OUTPUT_NAME}${__AE_LIB_SUFFIX}
        ${PROJECT_BINARY_DIR}/install/lib/${__OUTPUT_NAME}d${__AE_LIB_SUFFIX}
    CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
        -DCMAKE_DEBUG_POSTFIX=${CMAKE_DEBUG_POSTFIX}
        -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
        -DCMAKE_CONFIGURATION_TYPES=${CMAKE_CONFIGURATION_TYPES}
        -DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}
        -DBUILD_SHARED_LIBS=1
)

set_property(TARGET ${PROJECT_NAME} PROPERTY FOLDER ${__AE_EXTERNAL_FOLDER_NAME})