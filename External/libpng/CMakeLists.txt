__set_project()
set(__OUTPUT_NAME libpng16)
set_property(GLOBAL PROPERTY __PROJECT_LIBPNG_NAME ${PROJECT_NAME})
set_property(GLOBAL PROPERTY __PROJECT_LIBPNG_INCLUDE ${PROJECT_BINARY_DIR}/install/include)
set_property(GLOBAL PROPERTY __PROJECT_LIBPNG_LIB ${PROJECT_BINARY_DIR}/install/lib/${__OUTPUT_NAME}$<$<CONFIG:Debug>:d>${__AE_LIB_SUFFIX})
set_property(GLOBAL PROPERTY __PROJECT_LIBPNG_DLL ${PROJECT_BINARY_DIR}/install/$<IF:$<PLATFORM_ID:Windows>,bin,lib>/${__OUTPUT_NAME}$<$<CONFIG:Debug>:d>${__AE_DLL_SUFFIX})

get_property(__PROJECT_ZLIB_NAME GLOBAL PROPERTY "__PROJECT_ZLIB_NAME")
get_property(__PROJECT_ZLIB_INCLUDE GLOBAL PROPERTY "__PROJECT_ZLIB_INCLUDE")
get_property(__PROJECT_ZLIB_LIB GLOBAL PROPERTY "__PROJECT_ZLIB_LIB")

include(ExternalProject)

ExternalProject_Add(
    ${PROJECT_NAME}
    PREFIX ${PROJECT_BINARY_DIR}
    GIT_REPOSITORY https://github.com/glennrp/libpng.git
    GIT_TAG v1.6.37
    GIT_SHALLOW 1
    GIT_PROGRESS 1
    GIT_CONFIG core.autocrlf=false
    TMP_DIR tmp
    STAMP_DIR stamp
    DOWNLOAD_DIR download
    INSTALL_DIR install
    SOURCE_DIR src
    BINARY_DIR ${__BUILD_DIR}
    UPDATE_COMMAND ""
    BUILD_BYPRODUCTS 
        ${PROJECT_BINARY_DIR}/install/lib/${__OUTPUT_NAME}${__AE_LIB_SUFFIX}
        ${PROJECT_BINARY_DIR}/install/lib/${__OUTPUT_NAME}d${__AE_LIB_SUFFIX}
    CMAKE_ARGS
        -DCMAKE_DEBUG_POSTFIX=${CMAKE_DEBUG_POSTFIX}
        -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
        -DCMAKE_CONFIGURATION_TYPES=${CMAKE_CONFIGURATION_TYPES}
        -DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}
        -DPNG_BUILD_ZLIB=ON
        -DPNG_STATIC=OFF
        -DPNG_TESTS=OFF
        -DZLIB_INCLUDE_DIR=${__PROJECT_ZLIB_INCLUDE}
        -DZLIB_LIBRARY=${__PROJECT_ZLIB_LIB}
        -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
)

add_dependencies(${PROJECT_NAME} ${__PROJECT_ZLIB_NAME})
set_property(TARGET ${PROJECT_NAME} PROPERTY FOLDER ${__AE_EXTERNAL_FOLDER_NAME})